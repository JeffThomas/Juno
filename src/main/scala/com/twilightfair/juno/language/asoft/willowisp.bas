
1  PRINT "";""
10  REM *************************
11  REM ** WILL 'O THE WISP    **
12  REM ** BY MARK CAPELLA     **
13  REM ** COPYRIGHT (C) 1980  **
14  REM ** BY MICRO-SPARC, INC.**
15  REM ** LINCOLN, MA 01773   **
16  REM *************************
     
100  GOSUB 1000: REM  INSTS
110  GOSUB 2000: REM  SETUP
120  GOSUB 3000: REM  PLAY
190  SPEED= 255: END 
1000  REM   *** MAIN TITLES
1007  TEXT : NORMAL : NOTRACE : HOME 
     
1008  VTAB 3: HTAB 12: PRINT "WILL 'O THE WISP"
1009  VTAB 4: HTAB 12: PRINT "BY MARK CAPELLA"
1010  VTAB 6: PRINT "   YOU ARE A POOR COUNTRY BOY WHO IS    DESINED TO MARRY THE BEAUTIFUL AND KIND BRUNHILDE ON TOMORROW MORN. BUT BEING   YOUNG AND STUPID, YOU HAVE DECIDED TO   SPEND YOUR LAST DAY OF FREEDOM EXPLORING";
1020  PRINT "THE ENDLESS FOREST THAT SURROUNDS YOUR  SHACK. YOU COULD STAY HOME AND WATCH TV BUT THEN YOU'D NEVER GET LOST IN THE    FOREST AND FIND HIGH ADVENTURE AND LEARNTO BECOME A MAN AND WHAT THE MEANING OF LIFE IS."
1030  PRINT : PRINT "   NOW IN THE TRADITION OF ALL THESE    TYPE OF GAMES, I'M SURE YOU'LL GO ALONG WITH THE OBVIOUS AND GET YOURSELF LOST. SO NOW THAT I'M BUSY, WHY DON'T YOU DO  JUST THAT?? HAVE FUN ANYHOW..."
1980  VTAB 24: INPUT "HIT RETURN WHEN READY TO CONTINUE : ";ANS$
1985  HOME : VTAB 3: HTAB 12: PRINT "WILL 'O THE WISP": VTAB 6
1986  SPEED= 255: REM   SPEED
1990  RETURN 
2000  REM   *** SETUP STUFF HERE
2010  DIM FLG%(10): REM  SPECIAL FLAG LIST FOR INTERNAL USE
2020  DIM ITM%(10),ITM$(10),EX$(10): REM  OBJECT TRACKING    LIST FOR INTERNAL USE
2030  FOR I = 1 TO 9: READ ITM%(I),ITM$(I),EX$(I): NEXT : REM GET THE ITEM DESCRIPTIONS
2040  DATA  30,BOTTLE,AN EMPTY BOTTLE LIES DISCARDED NEARBY
2041  DATA  67,WATER,A SMALL POOL OF WATER COLLECTS HERE
2042  DATA   0,BROOM,A SMELLY WITCHES BROOM FLOATS NEARBY
2043  DATA  59,BALL,A MAGICAL CRYSTAL BALL GLISTENS HERE
2044  DATA  51,CARPET,A FLYING CARPET IS ROLLED UP HERE
2045  DATA  00,A GOLD WEDDING RING IS IN YOUR POCKET   FOR YOUR SWEET BRUNHILDE FROM THE GREAT MAGICIAN RALPH,RING
2046  DATA  75,GUANO,A PILE OF BAT GUANO SLOWLY ROTS HERE
2047  DATA  31,PEEL,AN OLD BANANA PEEL IS LYING IN THE DIRT
2048  DATA  10,CAN,AN EMPTY BEER CAN RUSTS HERE
2950 PSMZ = 1: REM  HOUSE
2955 TRUE = 1
2960 FALSE = 0
2990  RETURN 
3000  REM   *** LET'S PLAY !!!
3005  GOSUB 3100: REM  DESCRB
3006  IF PS = 66 THEN  GOSUB 12000: REM  RALPHS LIBRARY
3007  IF EOG THEN  RETURN 
3008  GOSUB 3200: REM  OBJCT
3010  GOSUB 4000: REM  INPUT
3015  GOSUB 5000: REM  SWITCH
3020  IF  NOT EOG THEN 3005
3090  RETURN 
3100  REM   *** DESCRIBE ROOM
3105  IF  INT ( RND (1) * 100) + 1 < 11 THEN  PRINT : PRINT "A SMALL WISP IS FLOATING HERE..."
3106 MVE = MVE + 1: IF MVE = 1 THEN PRINT : GOTO 3110
3107  IF MVE = 4 THEN MVE = 0
3108  RETURN 
3110  IF PS < 11 THEN  ON PS GOTO 40010,40020,40030,40040,40050,40060,40070,40080,40090,40100
3111  IF PS < 21 THEN  ON PS - 10 GOTO 40110,40120,40130,40140,40150,40160,40170,40180,40190,40200
3112  IF PS < 31 THEN  ON PS - 20 GOTO 40210,40220,40230,40240,40250,40260,40270,40280,40290,40300
3113  IF PS < 41 THEN  ON PS - 30 GOTO 40310,40320,40330,40340,40350,40360,40370,40380,40390,40400
3114  IF PS < 51 THEN  ON PS - 40 GOTO 40410,40420,40430,40440,40450,40460,40470,40480,40490,40500
3115  IF PS < 61 THEN  ON PS - 50 GOTO 40510,40520,40530,40540,40550,40560,40570,40580,40590,40600
3116  IF PS < 71 THEN  ON PS - 60 GOTO 40610,40620,40630,40640,40650,40660,40670,40680,40690,40700
3117  IF PS < 81 THEN  ON PS - 70 GOTO 40710,40720,40730,40740,40750,40760,40770,40780,40790,40800
3118  IF PS < 91 THEN  ON PS - 80 GOTO 40810,40820,40830,40840
3199  RETURN 
3200  REM   *** OBJECTS?
3205  PRINT 
3210  FOR I = 1 TO 9
3215  IF IT%(I) = PS THEN  PRINT ""EX$(I)
3220  NEXT : RETURN 
4000  REM   *** INPUT
4001  REM 
4002  REM 
4003  REM 
4010  PRINT : INPUT "";COM$
4015  IF COM$ = "" THEN 4010
4020  IF COM$ = "AGAIN" OR COM$ = "REPEAT" AND VERB$ <  > "" THEN RETURN 
4021  IF COM$ = "AGAIN" OR COM$ = "REPEAT" AND VERB$ = "" THEN  PRINT "I CAN'T LET YOU DO IT AGAIN, YOU HAVEN'TDONE ANYTHING YET!!!": GOTO 4010
4024 CP = 0
4025  GOSUB 4100: REM  VERB
4026 VERB$ = BY$
4030  GOSUB 4100: REM  NOUN
4031 NOUN$ = BY$
4035  IF VE$ = "NORTH" THEN VE$ = "N"
4036  IF VE$ = "SOUTH" THEN VE$ = "S"
4037  IF VE$ = "EAST" THEN VE$ = "E"
4038  IF VE$ = "WEST" THEN VE$ = "W"
4039  IF VE$ = "NORTHEAST" THEN VE$ = "NE"
4040  IF VE$ = "NORTHWEST" THEN VE$ = "NW"
4041  IF VE$ = "SOUTHEAST" THEN VE$ = "SE"
4042  IF VE$ = "SOUTHWEST" THEN VE$ = "SW"
4099  RETURN 
4100  REM   *** PARSE INPUT
4101  REM 
4102  REM 
4103  REM 
4104  REM 
4110  IF CP >  LEN (CO$) THEN BY$ = "": RETURN 
4115  FOR SP = CP + 1 TO  LEN (CO$): IF  MID$ (CO$,SP,1) <  > " " THEN  NEXT 
4120 BY$ =  MID$ (CO$,CP + 1,SP - CP - 1):CP = SP: IF BY$ = "THE" OR BY$ = "IT" OR BY$ = "A" OR BY$ = "AN" OR BY$ = "THAT" OR BY$ = "" THEN 4110
4125  IF BY$ = "JUMP" OR BY$ = "GO" OR BY$ = "TRAVEL" OR BY$ = "WALK" OR BY$ = "MOVE" THEN 4110
4190  RETURN 
5000  REM   *** SWITCH TO PROPER
5001  REM   *** ACTION SUBROUTINE
     
5006  IF (VE$ = "N" OR VE$ = "E" OR VE$ = "S" OR VE$ = "W" OR VE$ = "NE" OR VE$ = "NW" OR VE$ = "SE" OR VE$ = "SW") AND IT%(8) = PS THEN  PRINT "YOU SLIPPED AND FELL ON THE BANANA PEEL,WHY NOT TRY AGAIN?": RETURN 
5010  IF VE$ = "N" THEN 6005
5011  IF VE$ = "E" THEN 6010
5012  IF VE$ = "S" THEN 6015
5013  IF VE$ = "W" THEN 6020
5014  IF VE$ = "NE" THEN 6025
5015  IF VE$ = "NW" THEN 6030
5016  IF VE$ = "SE" THEN 6035
5017  IF VE$ = "SW" THEN 6040
5020  IF VE$ = "GET" OR VE$ = "TAKE" OR VE$ = "GRAB" OR VE$ = "CARRY" THEN 7000
5025  IF VE$ = "DROP" OR VE$ = "THROW" OR VE$ = "TOSS" OR VE$= "LEAVE" THEN 8000
5030  IF VE$ = "I" OR  LEFT$ (VE$,4) = "INVE" THEN 9000
5040  IF VE$ = "BREAK" OR VE$ = "SMASH" OR VE$ = "DESTROY" THEN 10000
5050  IF VE$ = "D" OR VE$ = "DOWN" THEN 11000
5060  IF VE$ = "IN" OR VE$ = "ENTER" THEN 13000
5070  IF VE$ = "OUT" OR VE$ = "EXIT" THEN 14000
5080  IF VE$ = "LOOK" OR VE$ = "SEE" OR VE$ = "VIEW" THEN MVE = 0: RETURN 
5090  IF VE$ = "SCORE" OR VE$ = "TOTAL" THEN  PRINT "YOU ARE STILL STUCK IN THE MAZES AREN'T YOU ??? WELL THAT'S ALL YOU NEED TO KNOWABOUT HOW YOU ARE DOING.": RETURN 
5100  IF VE$ = "HELP" THEN 15000
5110  IF VE$ = "END" OR VE$ = "QUIT" OR VE$ = "STOP" OR VE$ = "DONE" THEN 16000
5115  IF VE$ = "KILL" OR VE$ = "ATTACK" THEN  PRINT "YOU HAVE TO TELL ME HOW TO KILL     THE "NO$: RETURN 
5990  PRINT "WHAT???": RETURN 
6000  REM   *** MOVE AROUND MAZES
6005  IF N THEN PS = N: GOTO 6050
6006  GOTO 6045
6010  IF E THEN PS = E: GOTO 6050     
6011  GOTO 6045
6015  IF S THEN PS = S: GOTO 6050
6016  GOTO 6045
6020  IF W THEN PS = W: GOTO 6050     
6021  GOTO 6045
6025  IF NE THEN PS = NE: GOTO 6050
6026  GOTO 6045
6030  IF NW THEN PS = NW: GOTO 6050
6031  GOTO 6045
6035  IF SE THEN PS = SE: GOTO 6050
6036  GOTO 6045
6040  IF SW THEN PS = SW: GOTO 6050
6041  GOTO 6045
6045  PRINT "YOU CANNOT GO THAT WAY."
6046  IF PS = 36 THEN  PRINT "> BANG! < AS YOU HIT THE WALL, THE ROCKSWAKE UP AND NOTICE YOU IN THEIR REALM.  THE STONED YOU TO DEATH.":EOG = TRUE: RETURN 
6047  RETURN 
6050  REM  *** OK?
6055 MVE = 0: REM   DESC FLAG
6060  IF PS = 23 AND  NOT FLG%(1) THEN FLG%(1) = TR: PRINT : PRINT "*** BANG *** !!! AS YOU ENTER HERE, THE GOLD DISSAPEARS IN A FLASH, AND A HUGE  SET OF STEEL BARS DROPS INTO PLACE      LOCKING YOU INTO THE CAVES.": PRINT 
6070  IF PS = 84 AND  NOT FLG%(3) THEN  PRINT : PRINT "YOU'VE FOUND PRUDENCE !!!"
6100  IF PS = 16 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM THEEAST"
6101  IF PS = 18 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM THEEAST"
6102  IF PS = 19 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM THESOUTH"
6103  IF PS = 20 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM THESOUTHWEST"
6104  IF PS = 21 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM THESOUTHEAST"
6105  IF PS = 22 THEN  PRINT "A BLUE WILL 'O THE WISP BECKONS FROM    INSIDE THE CAVE"
6106  IF PS = 23 THEN  PRINT "A BLUE WILL 'O THE WISP DANCES OUTSIDE  THE CAVES ENTRANCE"
6199  IF ( NOT FLG%(2)) OR (FLG%(3)) THEN 6990
6200  IF PS = 30 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE NORTHEAST"
6201  IF PS = 37 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE NORTH"
6202  IF PS = 42 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE EAST"
6203  IF PS = 43 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE NORTHEAST"
6204  IF PS = 44 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE SOUTHEAST"
6205  IF PS = 45 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE SOUTHWEST"
6206  IF PS = 46 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE SOUTHWEST"
6207  IF PS = 47 THEN  PRINT "A GREEN WILL 'O THE WISP BECKONS FROM   THE SOUTH"
6990  RETURN 
7000  REM   *** GET AN OBJECT
7010  FOR I = 1 TO 9
7015  IF IT$(I) = NO$ THEN 7022
7020  NEXT : PRINT "THAT IS BEYOND YOUR POWER.": RETURN 
7022  IF IT%(I) =  - 1 THEN  PRINT "YOU ARE ALREADY CARRYING IT!!": RETURN 
7024  IF IT%(I) = 0 THEN  PRINT "YOU CHEATER!! YOU HAVE TO WORK FOR THAT": RETURN 
7025  IF IT%(I) <  > PS THEN  PRINT "I DO NOT SEE ANY "NO$" HERE.": RETURN 
7030  IF I = 2 AND IT%(1) <  >  - 1 THEN  PRINT "WHAT WILL YOU CARRY THE WATER IN?? YOUR HANDS?? YOU HAVE TO FIND A CONTAINER.": RETURN 
7990  PRINT "OK! WHY NOT?":IT%(I) =  - 1
7995  RETURN 
8000  REM   *** DROP OBJECT
8010  FOR I = 1 TO 9
8015  IF IT$(I) = NO$ THEN 8022
8020  NEXT : PRINT "THAT IS BEYOND YOUR POWER.": RETURN 
8022  IF IT%(I) <  >  - 1 THEN  PRINT "YOU ARE NOT CARRYING IT!!":RETURN 
8025  IF I = 1 AND IT%(2) =  - 1 THEN IT%(2) = PS: REM  IF DROP   BOTTLE, ALSO WATER
8990  PRINT "OK! WHY NOT?":IT%(I) = PS
8991  IF PS = 84 AND IT%(2) = 84 AND NOT FLG%(3) THEN  PRINT "AUURRRGGGHHHHH!!! SCREAMS THE WITCH !!! IN HER HASTE, THE WITCH FALLS INTO THE  BLAST FURNACE!! HELP !! I'M SMELTING... SMELTINGG... IS THE LAST SHE BREATHS."
8992  IF PS = 84 AND IT%(2) = 84 AND NOT FLG%(3) THEN FLG%(3) = TR:IT%(3) = PS:IT%(2) =  - 2
8995  RETURN 
9000  REM   *** INVENTORY
9005  PRINT 
9010  PRINT : PRINT "YOUR INVENTORY IS AS FOLLOWS": PRINT :TMP = 0
9015  FOR I = 1 TO 9
9020  IF IT%(I) =  - 1 THEN  PRINT ""IT$(I):TMP = TMP + 1
9025  NEXT : IF TMP = FALSE THEN PRINT "  (NOTHING)"
9990  RETURN 
10000  REM   *** BREAK SOMETHINE
10010  IF NO$ = "MIRROR" THEN 10100
10090  PRINT "HOW CAN I BREAK THAT FOR YOU?": RETURN 
10100  REM  *** BREAK MIRROR
10105  IF PS <  > 35 THEN  PRINT "I DON'T SEE ANY MIRROR HERE. PERHAPS IF YOU KEEP LOOKING YOU'LL FIND ONE.": RETURN 

10110  PRINT "THERE WAS SOMETHING BEHIND THAT MIRROR.  A HUNGRY TROLL WAS BEHIND THAT MIRROR!  HE LEPT OUT AND MADE YOU HIS SUPPER.    TOO BAD THOUGH, YOU JUST CAN'T TRUST A HUNGRY TROLL.":EOG = TR: RETURN 

11000  REM   *** DOWN ???
11010  IF PS <  > 61 THEN  PRINT "THERE IS NO WAY TO TRAVEL THROUGH THE   SOLID ROCK STUPID!!!": RETURN 
11090  PRINT "BOY ARE YOU DUMB. I TOLD YOU NOT TO JUMPAND NOW YOU'VE KILLED YOURSELF.":EOG = TR: RETURN 
12000  REM   *** MAGICIAN RALPH
12005  IF FLG%(2) THEN 12100
12010  PRINT : PRINT "RALPH LOOKS UP AT YOU SLOWLY... THE     ACCUMULATED WISDOM OF THE AGES PORING   FROM HIS SWEATY BROW. WELL?? HE CACKLES,WHAT IS IT THAT YOU WANT SO BADLY AS TO INTERRUPT ME FROM MY STUDIES????";
12015  INPUT ANS$: IF ANS$ = "FREEDOM" OR ANS$ = "HELP" OR ANS$ = "OUT" OR ANS$ = "HOME" THEN 12040
12020  PRINT : INPUT "WHAT??? HE SAYS... DO YOU WANT TO GET  OUT OF HERE???";ANS$
12025  IF  LEFT$ (ANS$,1) = "Y" THEN 12040
12030  PRINT : PRINT "WELL HE MUTTERS, SINCE I CAN'T SEEM TO  UNDERSTAND YOU... I'LL JUST KILL YOU!!  AND WITH THAT HE WAVES HIS HANDS IN A   MAGICAL MOTION AND YOU DISSAPEAR IN A   CLOUD OF BRIGHT ORANGE DUST.":EOG = TR: RETURN 
     
12040  IF ITM%(4) =  - 1 OR ITM%(5) =  - 1 THEN  PRINT : PRINT "I'D LIKE TO HELP YOU, BUT THAT IS MY    TREASURE THAT YOU HAVE THERE, AND I     DISLIKE A CLUMSY THIEF. SO SAY GOODBYE  QUICK CAUSE YOU'RE A GONER !!! AND WITH THAT HE KILLS YOU !!!":EOG = TR: RETURN 
     
12045  PRINT : PRINT "I'LL HELP YOU OUT... BUT FIRST I NEED A FAVOR. THERE IS A CERTAIN WITCH UP NORTHTHAT I ONCE ANGERED AND SHE HAS         ENCHANTED MY MAGIC STAFF AWAY FROM ME"
12050  PRINT : INPUT "SHE TURNED IT INTO A BROOMSTICK AND I   WANT IT BACK! SO IF YOU CAN STEAL IT    FROM HERE, I'LL SHOW YOU HOW TO GET OUT OF HERE. DO YOU WANT TO TRY???";ANS$
12055  IF  LEFT$ (ANS$,1) <  > "Y" THEN  PRINT : PRINT "WELL I NEVER!!! I TRY TO HELP YOU AND   YOU CAN'T HELP ME!!! WELL I'LL HELP YOU ALRIGHT... I'LL HELP YOU INTO A GRAVE !!AND WITH THAT HE PLUNGES A SWORD INTO   YOUR CHEST. YOU DIE.":EOG = TR: RETURN 
12060  PRINT : PRINT "GOOD!!! NOW AWAY YOU GO !!! BUT FIRST.. THE WAY INTO THE WITCHES LAND IS NOT    SIMPLE. ALL PATHS ARE CHARMED SO AS TO  ONLY LEAD AWAY FROM THE LAND."
12065  PRINT : PRINT "BUT IF YOU SIMPLY REVERSE THE TAUNTINGS OF THE GREEN WILL 'O THE WISP, YOU'LL   GET THERE EASILY ENOUGH. OK... NOW FOR  THE BIG FINISH !!!"
12070  PRINT : PRINT "THE MAGICIAN WAVES HIS HANDS, AND YOU   BLACK-OUT IN A FRENZY OF SENSATIONS. YOUAWAKE TO FIND..."
12075 PS = 36:FLG%(2) = TR:MV = 0 : GOSUB 3100
12080  RETURN 
12100  REM  *** SECOND TRIP
12104  SPEED= 120
12105  PRINT : PRINT "WELL MUTTERS RALPH, I SEE YOU'RE BACK.."
12106  SPEED= 255
12110  IF ITM%(4) =  - 1 OR ITM%(5) =  - 1 THEN  PRINT "AND TRYING TO STEAL MY TREASURE!!! BOY  NOW YOU'VE GONE AND DONE IT... I'M MAD!!AND A MAD WIZARD IS DANGEROUS!!! >POOF< YOU  ARE DEAD !!!":EOG = TR: RETURN 
12115  IF ITM%(3) <  >  - 1 THEN PRINT "AND EMPTY HANDED !!! WELL I JUST CAN'T  ABIDE ANY FREELOADERS. BEGONE GNAT !!!  AND WITH THAT HE CLAPS HIS HANDS, YOU   TURN INTO A GNAT, AND GET EATEN BY THE  NEAREST SPIDER.":EOG = TR: RETURN 
     
12119  SPEED= 120:IT%(6) =  - 1
12120  PRINT "AND WITH THE BROOM !! GOODY-GOODY HE    YELLS !! FOR FINDING MY STAFF I HEREBY  GRANT YOU YOUR FREEDOM AND A PRESENT !! CHECK YOUR BELONGINGS LATER HE CACKLES"
     
12125  PRINT : PRINT ">POOF!< YOU ARE STANDING UNDER A LARGE BEAUTIFUL SPREADING TREE.": GOSUB 9000: REM  INVENTORY
12130  PRINT : PRINT "YOUR BELOVED BRUNHILDE IS RUNNING       TOWARDS YOU THROUGH THE FIELD IN SLOW   MOTION... LIFE IS BEAUTIFUL ONCE AGAIN  AND ALL IS WELL. I HOPE YOU'VE ENJOYED  YOUR LITTLE EXCURSION.":EOG = TR: RETURN 
13000  REM   *** GO IN SOMETHING
13010  IF PS = 22 THEN VE$ = "SW": GOTO 6040
13020  IF PS = 7 THEN VE$ = "E": GOTO 6010
13090  PRINT "GO INTO WHAT ???": RETURN 
14000  REM   *** GO OUT SOMETHING
14010  IF PS = 23 THEN  PRINT "THE BARS ARE QUITE SOLID AND YOU ARE BUTA POOR COUNTRY BOY. YOU CANNOT GO OUT": RETURN 
14015  IF PS = 1 THEN VE$ = "W": GOTO 6020
14090  PRINT "GO OUT OF WHAT ???": RETURN 
15000  REM   *** HELP !!!!!
15010  PRINT : INPUT "DO YOU REALLY WANT A LITTLE HELP? ";ANS$: IF  LEFT$ (ANS$,1) <  > "Y" THEN  PRINT "WELL, SINCE NOT, BACK YOU GO.": RETURN 
15015  PRINT : PRINT "'GET' WILL LET YOU PICK UP AN OBJECT    'DROP' PUTS IT DOWN                     'INVENTORY' PRINTS YOUR SUPPLIES        'N' GOES NORTH, 'SW' GOES SOUTHWEST ETC 'END' LEAVES THE GAME"
15020  PRINT : PRINT "IF YOU ENCOUNTER A WITCH AND WISH TO    KILL HER (WHY NOT?) YOU HAVE TO FIGURE  OUT HOW. I WON'T TELL YOU, BUT IT WILL  INVOLVE SOME OBJECTS AND ACTIONS THAT   YOU WILL HAVE AVAILABLE. THAT'S ALL.": PRINT : RETURN 
     
16000  REM   *** END THE GAME
16010  PRINT : INPUT "DO YOU REALLY WISH TO QUIT NOW? ";ANS$: IF  LEFT$ (ANS$,1) <  > "Y" THEN  PRINT "WELL IF YOU'VE CHANGED YOUR MIND, THEN  BACK YOU'LL GO!": RETURN 
16020  PRINT : PRINT "OK, BUT YOU DIDN'T DO SO WELL."
16025  IF PS < 23 THEN  PRINT "YOU DIDN'T EVEN FIND THE CAVES!!!": GOTO 16050
16026  IF  NOT FLG%(2) THEN  PRINT "YOU DIDN'T MEET THE MAGICIAN !!!": GOTO 16050
16027  IF  NOT FLG%(3) THEN  PRINT "YOU NEVER KILLED THE WITCH !!!": GOTO 16050
16028  PRINT "YOU DIDN'T GET THE BROOM TO THE MAGICIAN": GOTO 16050
16050  PRINT : PRINT "SIGH... WELL I SUPPOSE YOU TRIED...":EOG = TR: RETURN 
40010  REM   *** ROOM NUMBER : 1
40013  PRINT "YOU ARE IN A SMALL FARMHOUSE. IT ISN'T  MUCH BUT YOU CALL IT HOME. IN FACT IT   ISN'T ANYTHING TO BE PROUD OF... IT IS  REALLY PRETTY SHABBY... ACTUALLY IT IS  DISGUSTING BUT YOU CALL IT HOME. ONE    DOOR EXITS TO THE WEST.
40018 N = 0:E = 0:S = 0:W = 7:NE = 0:NW = 0:SE = 0:SW = 0
40019  RETURN 
40020  REM   *** ROOM NUMBER : 2
40023  PRINT "YOU ARE IN THE FOREST STILL. IT LOOKS   LIKE YOU MAY BE LOST BUT YOU NEVER KNOW WITH THESE POOR FARM PEASANTS.
40028 N = 0:E = 0:S = 0:W = 0:NE = 4:NW = 3:SE = 0:SW = 0
40029  RETURN 
40030  REM   *** ROOM NUMBER : 3
40033  PRINT "YOU ARE IN A FOREST OF HUGE TREES. THE  FOREST IS FULL OF NUTS ...
40038 N = 0:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 2:SW = 5
40039  RETURN 
40040  REM   *** ROOM NUMBER : 4
40043  PRINT "IF YOU ARE NOT LOST YET THEN YOU KNOW   THAT YOU ARE IN THE FOREST. (DOES THAT  MAKE ANY SENSE?)
40048 N = 0:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 15:SW = 2
40049  RETURN 
40050  REM   *** ROOM NUMBER : 5
40053  PRINT "YOU ARE IN A FOREST OF HUGE TREES. THE  FOREST IS FULL OF NUTS ..
40058 N = 0:E = 0:S = 0:W = 0:NE = 3:NW = 0:SE = 0:SW = 6
40059  RETURN 
40060  REM   *** ROOM NUMBER : 6
40063  PRINT "THE FOREST IS ALL AROUND YOU. YOU ARE INTHE FOREST. IN THE FOREST YOU ARE. DO   YOU NEED ANY MORE HELP? YOU ARE IN THE  HUGE FOREST WITH THE REST OF THE NUTS.
40068 N = 0:E = 0:S = 8:W = 0:NE = 5:NW = 0:SE = 7:SW = 0
40069  RETURN 
40070  REM   *** ROOM NUMBER : 7
40073  PRINT "YOU ARE OUTSIDE YOUR DILAPIDATED RUNDOWNDIRTY LITTLE FARMHOUSE. YOU CAN BREATHE FRESH AIR AGAIN. A WELL WORN PATH LEADS TO THE NORTH AND INTO THE FOREST.
40078 N = 5:E = 1:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40079  RETURN 
40080  REM   *** ROOM NUMBER : 8
40083  PRINT "YOU ARE IN THE FOREST. GREAT MUSHROOMS  GROW HERE. THEY LOOK GOOD TO EAT BUT    COULD VERY WELL BE POISONOUS. I WOULD   NOT TRY TO EAT THEM IF I WERE YOU.
40088 N = 6:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 9:SW = 0
40089  RETURN 
40090  REM   *** ROOM NUMBER : 9
40093  PRINT "THE FOREST IS A LOVELY PLACE ISN'T IT?  IT HAS TREES AND SQUIRRELS AND NUTS AND BERRIES AND BUSHES AND YOU WHO ARE MOST LIKELY LOST!
40098 N = 0:E = 0:S = 0:W = 0:NE = 0:NW = 8:SE = 10:SW = 0
40099  RETURN 
40100  REM   *** ROOM NUMBER : 10
     
40103  PRINT "BOY YOU JUST KEEP WALKING DEEPER AND    DEEPER INTO THE FOREST. WHY DID YOU HAVETO LEAVE THAT NICE LITTLE RUN DOWN      SHANTY OF YOURS IN SEARCH OF ADVENTURE? IF YOU WANTED ADVENTURE THEN YOU ARE IN THE WRONG FANTASY.
40108 N = 0:E = 0:S = 0:W = 0:NE = 11:NW = 9:SE = 0:SW = 0
40109  RETURN 
40110  REM   *** ROOM NUMBER : 11
40113  PRINT "YOU ARE IN THE FOREST A LONG LONG WAY   FROM HOME. MONSTERS COULD BE ANYWHERE.  THEY COULD BE... BEHIND YOU NOW!!! HA-HAI'LL BET I SCARED YOU DIDN'T I??? WELL  TO TELL THE TRUTH YOU ARE JUST IN THE   FOREST WITH THE SQUIRRELS.
40118 N = 0:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 12:SW = 10
40119  RETURN 
40120  REM   *** ROOM NUMBER : 12
     
40123  PRINT "THE FOREST IS GETTING DARKER AND YOU AREOUT WAY PAST YOUR BEDTIME. MAYBE YOU    SHOULD TURN AROUND AND JUST GO HOME.    THAT IS IF YOUR SHACK IS STILL STANDING
     
40128 N = 0:E = 0:S = 0:W = 0:NE = 13:NW = 11:SE = 0:SW = 0
40129  RETURN 
40130  REM   *** ROOM NUMBER : 13
40133  PRINT "BOY IT IS REALLY DARK NOW... MONSTERS   COULD BE ANYWHERE... WHY NOT GO HOME TO YOUR MOTHER?
40138 N = 14:E = 0:S = 0:W = 9:NE = 17:NW = 0:SE = 0:SW = 0
40139  RETURN 
40140  REM   *** ROOM NUMBER : 14
40143  PRINT "AHA!! YOU ARE IN A CLEARING AND CAN SEE A TRAIL YOU ARE FAMILIAR WITH LEADING TOTHE SOUTH-WEST. OF COURSE THERE ARE     OTHER TRAILS BUT THIS SEEMS THE SMARTEST MOVE AT THIS TIME.
40148 N = 15:E = 0:S = 13:W = 0:NE = 0:NW = 0:SE = 0:SW = 7
40149  RETURN 
40150  REM   *** ROOM NUMBER : 15
     
40153  PRINT "WELL FOR THE HOPELESSLY LOST... YOU ARE IN THE LARGE DARK MEAN SCARY NASTY OLD  FOREST AND MOST LIKELY YOU'LL DIE HERE  AND GET EATEN BY THE ROTTEN SQUIRRELS.
40158 N = 0:E = 0:S = 14:W = 0:NE = 0:NW = 4:SE = 0:SW = 0
40159  RETURN 
40160  REM   *** ROOM NUMBER : 16
     
40163  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40168 N = 0:E = 18:S = 17:W = 0:NE = 0:NW = 15:SE = 0:SW = 0
40169  RETURN 
40170  REM   *** ROOM NUMBER : 17
     
40173  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40178 N = 16:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 13
40179  RETURN 
40180  REM   *** ROOM NUMBER : 18
     
40183  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40188 N = 15:E = 19:S = 17:W = 16:NE = 15:NW = 15:SE = 17:SW = 17
40189  RETURN 
40190  REM   *** ROOM NUMBER : 19
40193  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40198 N = 15:E = 15:S = 20:W = 18:NE = 15:NW = 15:SE = 15:SW = 17
40199  RETURN 
40200  REM   *** ROOM NUMBER : 20
     
40203  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40208 N = 19:E = 15:S = 15:W = 17:NE = 15:NW = 17:SE = 15:SW = 21
40209  RETURN 
40210  REM   *** ROOM NUMBER : 21
     
40213  PRINT "THE FOREST IS VERY DARK. MOST TRAILS    SEEM ALIKE AND YOU CANNOT GET YOUR      BEARINGS IN HERE.
40218 N = 17:E = 15:S = 0:W = 17:NE = 20:NW = 17:SE = 22:SW = 0
40219  RETURN 
40220  REM   *** ROOM NUMBER : 22
     
40223  PRINT "YOU'RE IN FRONT OF A LARGE CAVE ENTRANCEWHAT APPEARS TO BE A HUGE POT OF GOLD ISSITTING IN THE CAVE. YOU'LL BE RICH! YOUCAN GET YOUR MOTHER HER OPERATION! YOU  CAN BUY SHOES FOR YOUR SISTER! BOY ARE  YOU LUCKY!!!
40228 N = 15:E = 15:S = 15:W = 17:NE = 15:NW = 21:SE = 15:SW = 23
40229  RETURN 
40230  REM   *** ROOM NUMBER : 23
     
40233  PRINT "YOU ARE IN THE CAVE. THE EXIT IS BARED  BY HUGH STEEL BARS AND TRY AS YOU LIKE  YOU CANNOT GET THEM OPEN. NOW YOU'LL    MISS YOUR WEDDING AND BRUNHILDE IS GOINGTO CRY VERY LOUDLY WHEN SHE FINDS OUT.
40238 N = 0:E = 0:S = 24:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40239  RETURN 
40240  REM   *** ROOM NUMBER : 24
     
40243  PRINT "YOU ARE IN THE CAVES PROPER NOW. THERE  SEEM TO BE MANY DIFFERENT TUNNELS ABOUT HERE AND ONE LARGE ONE RUNS TO THE EAST.OF COURSE YOU DON'T HAVE TO LISTEN TO MEYOU COULD ALWAYS TRY TO GO WEST.
40248 N = 23:E = 28:S = 25:W = 26:NE = 0:NW = 0:SE = 0:SW = 0
     
40249  RETURN 
40250  REM   *** ROOM NUMBER : 25
     
40253  PRINT "WELL LETS SEE... THERE IS A TUNNEL HERE THAT RUNS EAST/WEST AND THERE IS A SMALL FISSURE BARELY MANAGEABLE THAT GOES TO  THE NORTH-EAST.
40258 N = 0:E = 28:S = 0:W = 26:NE = 24:NW = 0:SE = 0:SW = 0
40259  RETURN 
40260  REM   *** ROOM NUMBER : 26
     
40263  PRINT "THE TUNNEL BRANCHES OFF HERE INTO TWO   PATHS OTHER THAN THE ONE YOU JUST CAME  THROUGH. CONFUSING ISN'T IT???
40268 N = 24:E = 25:S = 27:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40269  RETURN 
40270  REM   *** ROOM NUMBER : 27
     
40273  PRINT "THE TUNNEL HERE TURNS SLIGHTLY OFF      COURSE. OH BY THE WAY. IF YOU WERE WOND-ERING ABOUT HOW YOU COULD SEE IF YOU ARETHIRTY OR FOURTY FEET UNDERGROUND... THEAIR IS FILLED WITH MAGIC AND IT GLOWS   ENOUGH FOR YOU TO SEE BY.
40278 N = 0:E = 26:S = 30:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40279  RETURN 
40280  REM   *** ROOM NUMBER : 28
     
40283  PRINT "YOU ARE AT A SMALL INTERSECTION OF ABOUTFOUR PASSAGES. WELL EXACTLY FOUR PASS-  AGES. I'M NOT GOING TO TELL YOU WHICH   WAY THEY GO !!
40288 N = 24:E = 0:S = 31:W = 25:NE = 0:NW = 0:SE = 0:SW = 29
     
40289  RETURN 
40290  REM   *** ROOM NUMBER : 29
     
40293  PRINT "YOU ARE AT THE INTERSECTION OF TWO SMALLPASSAGES. ONE LEADS NORTH-WEST AND THE  OTHER LEAVES TO THE SOUTH. ON THE WALL  IS SCRAWLED... 'DEATH TO THOSE WHO TAKE THE SOUTH PASSAGE'.
40298 N = 0:E = 0:S = 38:W = 0:NE = 0:NW = 26:SE = 0:SW = 0
40299  RETURN 
40300  REM   *** ROOM NUMBER : 30
     
40303  PRINT "NOT 'MANY' BUT QUITE A FEW PASSAGES     LEAVE THIS ROOM. CAN YOU FIND THEM??
40308 N = 27:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 36:SW = 37
40309  RETURN 
40310  REM   *** ROOM NUMBER : 31
     
40313  PRINT "THE ROOM HERE IS EXTREMELY DARK. I CAN  NOT TELL YOU ANYTHING EXCEPT THAT A     LARGE ROCK HERE IS REPELLING THE MAGIC  THAT GIVES YOU LIGHT. YOU HAVE ALSO BEEN BUMPING INTO WALLS NOW FOR ABOUT FIFTEENMINUTES.
40318 N = 0:E = 0:S = 33:W = 28:NE = 0:NW = 0:SE = 32:SW = 0
40319  RETURN 
40320  REM   *** ROOM NUMBER : 32
     
40323  PRINT "AHA! YOU GOT YOURSELF OUT OF THAT LAST  ROOM! GOOD! NOW YOU CAN FOLLOW THE NEXT TUNNEL AS IT LEADS YOU SOUTHWARD.
40328 N = 0:E = 0:S = 34:W = 0:NE= 0:NW = 0:SE = 0:SW = 0
40329  RETURN 
40330  REM   *** ROOM NUMBER : 33
     
40333  PRINT "THE ROOM HERE IS FILLED WITH DUST. IT   CLOGS YOUR EYES AND MAKES IT DIFFICULT  TO SEE. FOLLOW ME TO THE NORTH OR TO THESOUTH-WEST AND WE'LL GET OUT.
40338 N = 31:E = 0:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 35
40339  RETURN 
40340  REM   *** ROOM NUMBER : 34
     
40343  PRINT "A QUICK BEND IN THE PATH AND YOU NOW    HAVE TO TRAVEL A DIFFERENT WAY.
40348 N = 32:E = 0:S = 0:W = 39:NE = 0:NW = 0:SE = 0:SW = 0
40349  RETURN 
40350  REM   *** ROOM NUMBER : 35
     
40353  PRINT "THESE TUNNELS SEEM ENDLESS DON'T THEY?  WELL YOU MIGHT BE SURPRISED AT WHAT YOU CAN FIND IN HERE. MAYBE IF YOU BREAK THEBIG MIRROR ON THE WEST WALL YOU'D FIND  SOMETHING OF INTEREST BEHIND IT.
40358 N = 0:E = 0:S = 0:W = 0:NE = 33:NW = 0:SE = 0:SW = 38
40359  RETURN 
40360  REM   *** ROOM NUMBER : 36
     
40363  PRINT "YOU HAVE ENTERED A SMALL CHAMBER OF     SLEEPING ROCKS. THEY MUST BE SLEEPING   SINCE THEN ARE NOT MOVING. TIP-TOE      QUIETLY OUT AND YOU WON'T WAKE THEM.
40368 N = 0:E = 0:S = 41:W = 0:NE = 29:NW = 30:SE = 0:SW = 0
40369  RETURN 
40370  REM   *** ROOM NUMBER : 37
     
40373  PRINT "YOU ARE IN A SMALL EVEN WALLED CAVE ROOMA FEELING OF DIZZENESS CONFUSES YOUR    TRAVELS.
40378 N = 36:E = 36:S = 42:W = 0:NE = 36:NW = 30:SE = 36:SW = 0
40379  RETURN 
40380  REM   *** ROOM NUMBER : 38
     
40383  PRINT "WELL... THE MESSAGE IN THE LAST ROOM    MUST HAVE BEEN MISTAKEN... SINCE YOU ARESTILL ALIVE. WELL ONE CAN'T BELIVE ANY  GRAFITTI THEY READ ON WALLS CAN THEY?
40388 N = 0:E = 0:S = 0:W = 0:NE = 35:NW = 0:SE = 0:SW = 41
40389  RETURN 
40390  REM   *** ROOM NUMBER : 39
     
40393  PRINT "THREE PASSAGES LEAVE HERE. ONE TO THE   NORTH... ONE TO THE WEST... AND ONE TO  THE SOUTH. WHICH WAY WILL YOU TAKE?
40398 N = 34:E = 0:S = 40:W = 33:NE = 0:NW = 0:SE = 0:SW = 0
40399  RETURN 
40400  REM   *** ROOM NUMBER : 40
     
40403  PRINT "FOUR PATHS LEAVE HERE. LOUD MAJESTIC    SINGING SEEMS TO BE COMING FROM NEARBY. COULD THIS BE A SIGN? COULD IT LEAD OUT OF HERE? COULD YOU BE GOING HOME???
40408 N = 35:E = 39:S = 0:W = 41:NE = 0:NW = 0:SE = 0:SW = 48
     
40409  RETURN 
40410  REM   *** ROOM NUMBER : 41
     
40413  PRINT "THE PASSAGE TURNS A CORNER. THAT'S ALL  THIS ROOM RATES AS A DESCRIPTION.
40418 N = 36:E = 0:S = 0:W = 0:NE= 38:NW = 0:SE = 0:SW = 0
40419  RETURN 
40420  REM   *** ROOM NUMBER : 42
     
40423  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE.
40428 N = 37:E = 41:S = 41:W = 43:NE = 41:NW = 0:SE = 41:SW = 41
40429  RETURN 
40430  REM   *** ROOM NUMBER : 43
     
40433  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE.
40438 N = 42:E = 42:S = 41:W = 0:NE = 0:NW = 0:SE = 41:SW = 44
40439  RETURN 
40440  REM   *** ROOM NUMBER : 44
     
40443  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE.
40448 N = 43:E = 43:S = 0:W = 0:NE = 43:NW = 45:SE = 0:SW = 0
     
40449  RETURN 
40450  REM   *** ROOM NUMBER : 45
     
40453  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE. 
40458 N = 44:E = 43:S = 44:W = 44:NE = 46:NW = 44:SE = 44:SW = 0
40459  RETURN 
40460  REM   *** ROOM NUMBER : 46
     
40463  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE.
40468 N = 45:E = 43:S = 43:W = 44:NE = 47:NW = 44:SE = 43:SW = 45
40469  RETURN 
40470  REM   *** ROOM NUMBER : 47
     
40473  PRINT "THE TUNNELS IN HERE ARE VERY CONFUSING. YOU WILL NEVER GET OUT ALIVE.
40478 N = 67:E = 43:S = 43:W = 46:NE = 43:NW = 46:SE = 43:SW = 46
40479  RETURN 
40480  REM   *** ROOM NUMBER : 48
     
40483  PRINT "YOU ARE AT THE ENTRANCE TO A LARGE CLEANCASTLE. IT IS THE HOME OF THE ALL POWER-FUL ALL KNOWING ALL KIND ALL DRUNK      MAGICIAN RALPH. HE ALONE HAS THE POWER  TO GET YOU HOME TO SWEET BRUNHILDE.
40488 N = 40:E = 0:S = 49:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40489  RETURN 
40490  REM   *** ROOM NUMBER : 49
     
40493  PRINT "YOU ARE AT THE NORTH END OF THE GREAT   HALL. FINE PICTURES AND OIL PAINTINGS   ARE HUNG FROM THE WALLS. THIS MAGICIAN  SEEMS TO HAVE DONE WELL FOR HIMSELF.
40498 N = 48:E = 0:S = 50:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40499  RETURN 
40500  REM   *** ROOM NUMBER : 50
     
40503  PRINT "YOU ARE IN THE MIDDLE OF THE HALL. A    DOOR TO YOUR RIGHT LEADS INTO THE BLUE  ROOM AND ONE TO YOUR LEFT TO THE GREEN  ROOM.
40508 N = 49:E = 51:S = 53:W = 52:NE = 0:NW = 0:SE = 0:SW = 0
     
40509  RETURN 
40510  REM   *** ROOM NUMBER : 51
     
40513  PRINT "YOU ARE IN THE BLUE ROOM. A GREAT SENSE OF DEPRESSION FLOWS LIKE WAVES THROUGH  YOU. YOU LONG FOR HOME AND YOUR SWEET   BRUNHILDE. HOW YOU WISH YOU WERE THERE.
     
40518 N = 0:E = 0:S = 0:W = 50:NE = 0:NW = 0:SE = 0:SW = 0
40519  RETURN 
40520  REM   *** ROOM NUMBER : 52
     
40523  PRINT "YOU ARE IN THE GREEN ROOM. YOU ARE SUDD-ENLY VERY JEALOUS OF RALPH AND HIS FINE CASTLE. HOW COME HE RATES ALL THIS STUFFAND YOU LIVE IN A HOLE IN THE GROUND?!?
     
40528 N = 0:E = 50:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40529  RETURN 
40530  REM   *** ROOM NUMBER : 53
     
40533  PRINT "YOU ARE NOW AT THE SOUTH END OF THE     GREAT HALL. HALLWAYS LEAVE TO THE NORTH AND TO THE EAST AND TO THE WEST.
40538 N = 50:E = 54:S = 0:W = 61:NE = 0:NW = 0:SE = 0:SW = 0
40539  RETURN 
40540  REM   *** ROOM NUMBER : 54
     
40543  PRINT "YOU ARE IN THE MIDDLE OF THE EAST HALL. A RAT SCURRIES PAST YOU AND TAKES A NIP AT YOUR LEG. IN AN INSTANT HE IS GONE   WITH A SNEER AND A TWITCH OF HIS TAIL.
40548 N = 0:E = 55:S = 0:W = 53:NE = 0:NW = 0:SE = 0:SW = 0
40549  RETURN 
40550  REM   *** ROOM NUMBER : 55
     
40553  PRINT "YOU ARE AT THE END OF THE EAST HALL. THEONLY NEW PASSAGE IS A SMALL SERVANTS    EXIT TO THE SOUTH-EAST.
     
40558 N = 0:E = 0:S = 0:W = 54:NE = 0:NW = 0:SE = 56:SW = 0
40559  RETURN 
40560  REM   *** ROOM NUMBER : 56
     
40563  PRINT "YOU ARE IN THE SERVANT'S AREA. SINCE    RALPHS LAST SERVANT DIED NINE-HUNDRED   YEARS AGO THE PLACE IS A MESS AND ALSO  SMELLS PRETTY BAD.
40568 N = 0:E = 0:S = 0:W = 57:NE = 55:NW = 0:SE = 0:SW = 0
40569  RETURN 
40570  REM   *** ROOM NUMBER : 57
     
40573  PRINT "YOU ARE OUTSIDE THE SERVANTS QUARTERS.  A DISTRESSING SMELL COMES FROM THE EAST.TIS BEST TO GO BACK TO THE WEST FOR YOURNOSES SAKE.
     
40578 N = 0:E = 56:S = 0:W = 58:NE = 0:NW = 0:SE = 0:SW = 0
40579  RETURN 
40580  REM   *** ROOM NUMBER : 58
     
40583  PRINT "YOU ARE IN THE MIDDLE OF THE SOUTH HALL.AN EERIE LIGHT COMES FROM THE WEST AND  GREAT MUSIC FILLS THE AIR.
40588 N = 0:E = 57:S = 0:W = 59:NE = 0:NW = 0:SE = 0:SW = 0
40589  RETURN 
40590  REM   *** ROOM NUMBER : 59
     
40593  PRINT "YOU ARE AT THE END OF THE SOUTH HALL.   TWO SMALL PASSAGES LEAVE HERE. ONE TO   THE NORTH-WEST AND ONE TO THE SOUTH-WEST
40598 N = 0:E = 58:S = 0:W = 0:NE = 0:NW = 60:SE = 0:SW = 62
40599  RETURN 
40600  REM   *** ROOM NUMBER : 60
     
40603  PRINT "YOU ARE AT THE END OF THE WEST HALL. A  SMALL PASSAGE LEADS TO THE SOUTH-WEST.
40608 N = 0:E = 61:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 59
40609  RETURN 
40610  REM   *** ROOM NUMBER : 61
     
40613  PRINT "YOU ARE IN THE MIDDLE OF THE WEST HALL. A LARGE HOLE IN THE FLOOR REVEALS A HUGEDROP TO THE GROUND HUNDREDS OF FEET     BELOW YOU. BETTER NOT DO DOWN...
40618 N = 0:E = 53:S = 0:W = 60:NE = 0:NW = 0:SE = 0:SW = 0
40619  RETURN 
40620  REM   *** ROOM NUMBER : 62
     
40623  PRINT "YOU ARE IN A WINDING PASSAGE. AS YOUR   TRAVEL PROGRESSES THE EERIE MUSIC GETS  LOUDER AND YOU GET COLDER BUT THIS COULDBE THE WAY OUT!!!
40628 N = 0:E = 59:S = 0:W = 0:NE = 0:NW = 63:SE = 0:SW = 0
40629  RETURN 
40630  REM   *** ROOM NUMBER : 63
     
40633  PRINT "YOU ARE IN A CONNECTING HALLWAY WITH    SOME LOUD ALBEIT EERIE MUSIC EMANATING  FROM THE SOUTH-WEST.
40638 N = 0:E = 0:S = 0:W = 0:NE = 62:NW = 0:SE = 0:SW = 64
40639  RETURN 
40640  REM   *** ROOM NUMBER : 64
     
40643  PRINT "YOU ARE IN A SMALL ANTE-CHAMBER. A LARGEWELL USED PASSAGE LEADS TO THE NORTH ANDA SMALLER PASSAGE TO THE WEST. EERIE    MUSIC COMES FROM THE NORTH.
40648 N = 65:E = 0:S = 0:W = 66:NE = 0:NW = 0:SE = 63:SW = 0
40649  RETURN 
40650  REM   *** ROOM NUMBER : 65
     
40653  PRINT "YOU OPEN THE DOOR AND ENTER INTO.....   RALPHS BATHROOM. A RADIO TUNED TO ONE OFTHE MORE POPULAR ROCK STATIONS IS BLAST-ING FORTH PUNK ROCK MUSIC. THE EFFECT ITHAS ON THE CASTLES WALLS IS EERIE INDEED
40658 N = 0:E = 0:S = 64:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40659  RETURN 
40660  REM   *** ROOM NUMBER : 66
     
40663  PRINT "YOU ARE IN THE LIBRARY WHERE THE GOOD   MAGICIAN RALPH IS POORING OVER AN OLD   AND ANCIENT TOME. THE WORD YOBYALP SHOWSTHROUGH THE COVER FROM THE LIGHT BEHIND.RALPH IS DEEPLY ABSORBED IN THE BOOK.
40668 N = 0:E = 64:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40669  RETURN 
40670  REM   *** ROOM NUMBER : 67
     
40673  PRINT "YOU ARE AT THE EDGE OF THE EVIL WITCHES LAND. THE EVIL PRUDENCE DOES NOT SEEM TOBE ANYWHERE AROUND. THAT IS GOOD FOR YOUSINCE SHE COULD TURN YOU INTO AN UGLIER TOAD THAN YOU ALREADY ARE.
40678 N = 68:E = 47:S = 73:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40679  RETURN 
40680  REM   *** ROOM NUMBER : 68
     
40683  PRINT "WALKING NORTH YOU WILL FIND A TWISTING  OLD TRAIL. TO THE WEST LIES CONFUSION.  SOUTH IS THE WAY TO GO FOR A FAST EXIT  FROM THIS LAND. GO THERE IF YOU VALUE   YOUR LIFE.
40688 N = 69:E = 0:S = 67:W = 70:NE = 0:NW = 0:SE = 0:SW = 0
40689  RETURN 
40690  REM   *** ROOM NUMBER : 69
     
40693  PRINT "THERE IS A TINY LITTLE HIDDEN TWISTING  SECRET OLD OVERGROWN DIRTY SNEAKY TRAIL THAT LEADS WEST FROM HERE. SOUTH IS MUCH NICER BUT NOT A LOT OF FUN. A ROCK HERE SAYS 'THIS WAY TO THE TLHTSOODST' AND IT POINTS WEST.
40698 N = 0:E = 0:S = 68:W = 72:NE = 0:NW = 0:SE = 0:SW = 0
40699  RETURN 
40700  REM   *** ROOM NUMBER : 70
     
40703  PRINT "YOU ARE IN A SMALL CLEARING. A PAPER    LIES NEARBY. IT READS 'CONFUSION SAYS-  EXPLORER WHO GETS OUT... IS VERY LUCKY  INDEED...'
40708 N = 70:E = 70:S = 70:W = 70:NE = 70:NW = 70:SE = 70:SW = 74
40709  RETURN 
40710  REM   *** ROOM NUMBER : 71
     
40713  PRINT "YOU ARE IN THE MIDDLE OF A LONG BORING  NORTH/SOUTH PATHWAY. YOUR HEAD IS       SLIGHTLY ACHING.
40718 N = 72:E = 0:S = 70:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40719  RETURN 
40720  REM   *** ROOM NUMBER : 72
     
40723  PRINT "YOU ARE IN THE SNEAKY CLEARING. THERE   ARE THREE EXITS FROM HERE. ONE THAT GOESTO THE SOUTH... ONE TO THE SOUTH/WEST...AND A DIRTY SNEAKY.....LITTLE TRAIL TO  THE NORTH.
40728 N = 69:E = 0:S = 71:W = 0:NE = 0:NW = 0:SE = 0:SW = 76
40729  RETURN 
40730  REM   *** ROOM NUMBER : 73
     
40733  PRINT "A SMALL CLEARING AT THE SOUTHERN EDGE OFTHE EVIL PRUDENCES LAND GIVES YOU ROOM  TO REST AND RELAX. TWO PATHS LEAD AWAYFROM HERE.
40738 N = 70:E = 0:S = 0:W = 0:NE = 0:NW = 77:SE = 0:SW = 0
40739  RETURN 
40740  REM   *** ROOM NUMBER : 74
     
40743  PRINT "AN INTERSECTION OF THREE PATHS IS HERE. YOUR HEAD ACHES SLIGHLTY AS YOU APPROACHTHE AREA. YOU CAN LEAVE WITH MY BLESSINGIF YOU FIND THE WAY OUT.
40748 N = 75:E = 0:S = 78:W = 0:NE = 70:NW = 0:SE = 0:SW = 0
40749  RETURN 
40750  REM   *** ROOM NUMBER : 75
     
40753  PRINT "THE PATHS ORIENTATION BECOMES SUBTLY    ALTERED AS YOU ROUND YON CORNER. WATCH  OUT FOR THE BAT GUANO TO YOUR LEFT.
40758 N = 0:E = 0:S = 74:W = 0:NE = 0:NW = 79:SE = 0:SW = 0
40759  RETURN 
40760  REM   *** ROOM NUMBER : 76
     
40763  PRINT "THE MIDPOINT FOR A LONG SLOPING CORRIDORIS WHERE YOU ARE. NOT MUCH SPECIAL ABOUTTHIS PLACE EXCEPT THE SMELL OF BAT GUANO
40768 N = 0:E = 0:S = 0:W = 0:NE = 72:NW = 0:SE = 0:SW = 79
40769  RETURN 
40770  REM   *** ROOM NUMBER : 77
     
40773  PRINT "YOU ARE AT THE MIDPOINT OF A LONG LONG  SLOPING CORRIDOR LEADING SLIGHTLY SOUTH
40778 N = 0:E = 0:S = 0:W = 0:NE = 74:NW = 81:SE = 73:SW = 0
40779  RETURN 
40780  REM   *** ROOM NUMBER : 78
     
40783  PRINT "THE PATH HERE IS LITTERED WITH THE FRESHREMAINS OF BATS AND TOADS. THEIR BODIES ARE STILL DECAYING IN THE DIRT.
40788 N = 0:E = 0:S = 74:W = 82:NE = 0:NW = 0:SE = 0:SW = 0
40789  RETURN 
40790  REM   *** ROOM NUMBER : 79
     
40793  PRINT "YOU ARE AT THE INTERSECTION OF THREE    TRAILS. ONE TO THE WEST AND ONE TO THE  NORTH/EAST AND ONE TO THE SOUTH/EAST.   WHEN YOUR COMPASS WORKS... IT SURE IS A HANDY THING TO HAVE.
40798 N = 0:E = 0:S = 0:W = 83:NE = 76:NW = 0:SE = 75:SW = 0
40799  RETURN 
40800  REM   *** ROOM NUMBER : 80
     
40803  PRINT "YOU ARE OVERCOME BY THE FEELING OF      NAUSEA. AS YOU TRY TO ALIGN YOURSELF ON YOUR COMPASS YOU ROCK AND REEL BACK AND FORTH AND FIND IT DIFFICULT TO WALK IN ASTRAIGHT LINE. WHAT COULD BE CAUSING    THIS TO HAPPEN???
40808 N = 71:E = 72:S = 79:W = 77:NE = 69:NW = 84:SE = 76:SW = 82
40809  RETURN 
40810  REM   *** ROOM NUMBER : 81
     
40813  PRINT "AT THE SOUTH-WEST EDGE OF THE WITCHES   LAND THE TUNNEL TURNS SLIGHTLY. EITHER  THAT OR WALK INTO THE HUGE ROCK THAT IS IN ITS WAY.

40818 N = 82:E = 0:S = 0:W = 0:NE= 0:NW = 0:SE = 77:SW = 0
40819  RETURN 
40820  REM   *** ROOM NUMBER : 82
     
40823  PRINT "OOPS!!! A SHARP TURN FORCES YOU TO GO   EITHER SOUTH OR EAST DEPENDING ON WHICH WAY YOU CAME FROM.
40828 N = 0:E = 78:S = 81:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40829  RETURN 
40830  REM   *** ROOM NUMBER : 83
     
40833  PRINT "A ROOM HERE HAS AN EASTERN EXIT AND ALSOA NORTHERN ONE. FROM THE NORTH COMES A  FUNNY FEELING OF DIZZINESS.
40838 N = 80:E = 79:S = 0:W = 0:NE = 0:NW = 0:SE = 0:SW = 0
40839  RETURN 
40840  REM   *** ROOM NUMBER : 84
     
40843  PRINT "YOU ARE AT THE EVIL WITCH PRUDENCES OLDEBLACKSMITHE SHOPPE. HERE WITH HER BLASTEFURNACEE IS WHERE SHE FORGES HER HORSES SHOES FOR HER HUNTING RAIDES. THE HEAT  FROM THE FURNACE BURNS TO YOUR SOUL.
40848 N = 0:E = 0:S = 0:W = 0:NE = 80:NW = 0:SE = 0:SW = 0
40849  RETURN 
